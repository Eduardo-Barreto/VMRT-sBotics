numero angulo = 0
booleano verCurva = falso

tarefa verificaCurva{
    frente(150)
    esperar(350)
    se(cor(2) == "BRANCO" e cor(3) == "BRANCO" e cor(4) == "BRANCO")entao{
        verCurva = verdadeiro
    }
    senao{
        verCurva = falso
    }
}

tarefa ajuste{
    #Primeiro Quadrante
    se((direcao() > 0 ou direcao() < 360) e direcao() <= 45)entao{
        angulo = direcao()
        rotacionar(300, negativo(angulo))
    }
    #Segundo Quadrante
    senao se(direcao() > 45 e direcao() <= 90)entao{
        angulo = 90 - direcao()
        rotacionar(300, angulo)
    }
    #Terceiro Quadrante
    senao se(direcao() > 90 e direcao() <= 135)entao{
        angulo = 90 - direcao()
        rotacionar(300, angulo)
    }
    #Quarto Quadrante
    senao se(direcao() > 135 e direcao() <= 180)entao{
        angulo = 180 - direcao()
        rotacionar(300, angulo)
    }
    #Quinto Quadrante
    senao se(direcao() > 180 e direcao() <= 225)entao{
        angulo = 180 - direcao()
        rotacionar(300, angulo)
    }
    #Sexto Quadrante
    senao se(direcao() > 225 e direcao() <= 270)entao{
        angulo = 270 - direcao()
        rotacionar(300, angulo)
    }
    #Sétimo Quadrante
    senao se(direcao() > 270 e direcao() <= 315) entao{
        angulo = 270 - direcao()
        rotacionar(300, angulo)
    }
    #Oitava Quadrante
    senao se(direcao() > 315 e direcao() <= 360)entao{
        angulo = 360 - direcao()
        rotacionar(300, angulo)
    }
}

tarefa curvaEsquerda{
    escrever(1, "Curva pra Esquerda")
    frente(100)
    esperar(500)
    esquerda(1000)
    esperar(300)

    zerartemporizador()
    enquanto (cor(3) != "PRETO") farei {
        se(temporizador() > 3420)entao{
            interromper()
        }
        esquerda(1000)
    }
    parar()
    esperar(100)
    limparconsole()
}

tarefa curvaDireita{
    escrever(1, "Curva pra Direita")
    frente(100)
    esperar(500)
    direita(1000)
    esperar(300)

    zerartemporizador()
    enquanto (cor(3) != "PRETO") farei {
        se(temporizador() > 3420)entao{
            interromper()
        }
        direita(1000)
    }
    parar()
    esperar(100)
    limparconsole()
}

tarefa seguirLinha{
    se(cor(1) == "PRETO" ou cor(2) == "PRETO")entao{
        escrever(1, "um tequinho pra direita")
        direita(1000)
    }
    se(cor(4) == "PRETO" ou cor(5) == "PRETO")entao{
        escrever(1, "um tequinho pra esquerda")
        esquerda(1000)
    }
    se(cor(1) == "BRANCO" e cor(2) == "BRANCO" e cor(4) == "BRANCO" e cor(5) == "BRANCO")entao{
        escrever(1, "continue a nadar, continue a nadar...")
        frente(100)
    }
}

inicio
    velocidadeatuador(150)
    levantar(600)

#--------------------PISO--------------------
    enquanto ((ultra(2) > 20) e inclinacao()<340 ou inclinacao()>350) farei {
        
        se(ultra(3)<20) entao{
            se(ultra(1)>30 e ultra(1)<70)entao{
                interromper()
            }
            escrever(1, "Obstáculo")
            ajuste()
            direita(1000)
            esperar(1840)
            frente(300)
            esperar(600)
            esquerda(1000)
            esperar(1840)
            frente(300)
            esperar(600)
            esquerda(1000)
            esperar(1840)
            enquanto (cor(2) != "PRETO" e cor(3) != "PRETO" e cor(4) != "PRETO") farei {
                frente(200)
            }
            frente(300)
            esperar(250)
            curvaDireita()
            ajuste()
            enquanto (!toque(1)) farei {
              tras(250)
            }
        }
    #--------------------SEGUIDOR-----------------
        seguirLinha()

        se(cor(1) == "PRETO" e cor(2) == "PRETO" e cor(4) == "PRETO" e cor(5) == "PRETO")entao{
            escrever(1, "Encruzilhada")
            frente(150)
            esperar(350)
        }

    #------------------CURVAS-------------------
    #------------------VERDE---------------------
        se (cor(1) == "VERDE" ou cor(2) == "VERDE") entao {
            escrever(2, "VERDE")
            frente(150)
            esperar(350)
            curvaDireita()
        }

        se (cor(4) == "VERDE" ou cor(5) == "VERDE") entao {
            escrever(2, "VERDE")
            frente(150)
            esperar(350)
            curvaEsquerda()
        }
    #-------------------PRETO---------------------
        se (cor(1) == "PRETO" e cor(2) == "PRETO") entao {
            escrever(2, "PRETO")
            verificaCurva()
            se (verCurva) entao {
                curvaDireita()
            }
        }

        se (cor(4) == "PRETO" e cor(5) == "PRETO") entao {
            escrever(2, "PRETO")
            verificaCurva()
            se (verCurva) entao {
                curvaEsquerda()
            }
        }

    }

#------------------RAMPA------------------
    limparconsole()
    escrever(3, "subindo rampa")
    velocidadeatuador(150)
    baixar(400)

    enquanto (ultra(2)>20) farei {
        seguirLinha()
    }
    enquanto (ultra(2)<20) farei {
        seguirLinha()
    }
    parar()
    limparconsole()

#----------------RESGATE-----------------
    velocidadeatuador(150)
    baixar(200)
    frente(300)
    esperar(300)

    direita(1000)
    esperar(3420)

    zerartemporizador()

    enquanto(verdadeiro)farei{
        frente(300)
        baixar(600)
        esperar(2500)

        direita(1000)
        esperar(3420)
        levantar(600)
    }

fim